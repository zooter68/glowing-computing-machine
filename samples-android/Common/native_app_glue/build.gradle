apply plugin: "com.android.model.native"

def ndkDir = null

if (project.rootProject.file('local.properties').exists()) {
    // Retrieve ndk path: ndk.dir MUST be set in file local.properties
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    ndkDir = properties.getProperty('ndk.dir')
} else if (System.getProperty('ANDROID_NDK_HOME') != null) {
    ndkDir = System.getProperty('ANDROID_NDK_HOME')
} else if (System.getenv('ANDROID_NDK_HOME') != null) {
    ndkDir = System.getenv('ANDROID_NDK_HOME')
}

if (ndkDir == null || ndkDir == "") {
    throw new Exception("Need to run from Android Studio or define ANDROID_NDK_HOME")
}

model {
    android {
        compileSdkVersion = 23
    }

    android.ndk {
        abiFilters.addAll(["armeabi", "armeabi-v7a", "x86", "arm64-v8a"])
        moduleName = "native_app_glue"
    }

    android.sources {
        main {
            jni {
                exportedHeaders {
                    srcDir "${ndkDir}/sources/android/native_app_glue"
                }
                source {
                    srcDir "${ndkDir}/sources/android/native_app_glue"
                }
            }
        }
    }
}

/*
    We are building a static library, so disable all the shared library tasks.
*/
tasks.whenTaskAdded { task ->
    if (task.name.contains("SharedLibrary")) {
        task.enabled = false
    }
}
